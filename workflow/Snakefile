configfile: "config/config.yaml"

all_posteriors = ["results/posteriors/image_posterior"+str(i)+".csv" for i in range(int(config["ntest"]))]+["results/posteriors/tabc_posterior"+str(i)+".csv" for i in range(int(config["ntest"]))]
posterior_plots = ["results/plots/image_posterior"+str(i)+".pdf" for i in range(int(config["ntest"]))]+["results/plots/tabc_posterior"+str(i)+".pdf" for i in range(int(config["ntest"]))]
bench = ["results/benchmark.csv","results/benchmarkMean.csv"] #"results/plots/Entropy.pdf","results/plots/RSSE.pdf"]
pred = ["results/plots/predictive/image_pred_"+str(i)+"_"+str(j)+".pdf" for i in range(int(config["ntest"])) for j in range(int(config["npred"]))]+["results/plots/predictive/tabc_pred_"+str(i)+"_"+str(j)+".pdf" for i in range(int(config["ntest"])) for j in range(int(config["npred"]))]
test_sims = ["results/plots/test_simulated_"+str(i)+".pdf" for i in range(int(config["ntest"]))]
test_pimages = ["results/plots/test_pimage_"+str(i)+"_dim_0.pdf" for i in range(int(config["ntest"]))]

rule all:
	input: bench+posterior_plots+pred+test_sims+test_pimages

rule priorSamples:
	output: "results/abcPriorSamples.csv"
	params:
		nsamples=config["nsamp"]
	conda: "envs/R.yaml"
	script: "scripts/prior.R"

rule testSamples:
	output: "results/testSamples.csv"
	params:
		nsamples=config["ntest"]
	conda: "envs/R.yaml"
	script: "scripts/prior.R"

rule abcSimulations:
	input: "results/{params}Samples.csv"
	params:
		index="{i}"
	output: "results/persistence/{params}_pdata_{i}.npy","results/simulated/{params}_simdata_{i}.npy"
	conda: "envs/Python.yaml"
	script: "scripts/simulation.py"

rule persistenceImages:
	input: "results/persistence/{params}_pdata_{i}.npy"
	output: "results/persistence/{params}_pimage_{i}.csv"
	conda: "envs/Python.yaml"
	script: "scripts/persistenceImage.py"

rule pimagePriorConc:
	input: ["results/persistence/abcPrior_pimage_"+str(i)+".csv" for i in range(int(config["nsamp"]))]
	output: "results/abcstats/abcPrior_persistenceStats.csv"
	shell: "cat {input} > {output}"

rule pimageTestConc:
	input: ["results/persistence/test_pimage_"+str(i)+".csv" for i in range(int(config["ntest"]))]
	output: "results/abcstats/test_persistenceStats.csv"
	shell: "cat {input} > {output}"

rule persistencePost:
	input: "results/abcPriorSamples.csv","results/testSamples.csv","results/abcstats/abcPrior_persistenceStats.csv","results/abcstats/test_persistenceStats.csv"
	output: ["results/posteriors/tabc_posterior"+str(i)+".csv" for i in range(int(config["ntest"]))]
	conda: "envs/R.yaml"
	script: "scripts/samplePosterior.R"

rule imageStats:
	input: "results/simulated/{params}_simdata_{i}.npy"
	output: "results/image/{params}_image_{i}.csv"
	conda: "envs/Python.yaml"
	script: "scripts/imageStats.py"

rule imagePriorConc:
	input: ["results/image/abcPrior_image_"+str(i)+".csv" for i in range(int(config["nsamp"]))]
	output: "results/abcstats/abcPrior_imageStats.csv"
	shell: "cat {input} > {output}"

rule imageTestConc:
	input: ["results/image/test_image_"+str(i)+".csv" for i in range(int(config["ntest"]))]
	output: "results/abcstats/test_imageStats.csv"
	shell: "cat {input} > {output}"

rule imagePost:
	input: "results/abcPriorSamples.csv","results/testSamples.csv","results/abcstats/abcPrior_imageStats.csv","results/abcstats/test_imageStats.csv"
	output: ["results/posteriors/image_posterior"+str(i)+".csv" for i in range(int(config["ntest"]))]
	conda: "envs/R.yaml"
	script: "scripts/samplePosterior.R"

# Posterior entropy, RSSE
rule posteriorBenchPlot:
	input: ["results/testSamples.csv"]+all_posteriors
	output: "results/plots/Entropy.pdf","results/plots/RSSE.pdf"
	conda: "envs/R.yaml"
	script: "scripts/plotBench.R"

rule posteriorBenchTables:
	input: ["results/testSamples.csv"]+all_posteriors
	output: "results/benchmark.csv","results/benchmarkMean.csv",
	conda: "envs/R.yaml"
	script: "scripts/calcBench.R"

# Posterior plot
rule posteriorPlot:
	input: "results/posteriors/{params}_posterior{i}.csv","results/testSamples.csv"
	params:
		index="{i}"
	output: "results/plots/{params}_posterior{i}.pdf"
	conda: "envs/R.yaml"
	script: "scripts/plotPosterior.R"

# Posterior predictive sampling
rule posteriorPred:
	input: "results/posteriors/{params}_posterior{i}.csv"
	output: "results/predictive/{params}_predictive{i}.csv"
	conda: "envs/R.yaml"
	script: "scripts/predictive.R"

rule predictiveSimulations:
	input: "results/predictive/{params}_predictive{i}.csv"
	params:
		index="{j}"
	output: "results/predictive/persistence/{params}_pdata_{i}_{j}.npy","results/predictive/simulated/{params}_simdata_{i}_{j}.npy"
	conda: "envs/Python.yaml"
	script: "scripts/simulation.py"

rule plotPred:
	input: "results/predictive/simulated/{params}_simdata_{i}_{j}.npy"
	output: "results/plots/predictive/{params}_pred_{i}_{j}.pdf"
	conda: "envs/Python.yaml"
	script: "scripts/plotSim.py"

rule plotSim:
	input: "results/simulated/{params}_simdata_{i}.npy"
	output: "results/plots/{params}_simulated_{i}.pdf"
	conda: "envs/Python.yaml"
	script: "scripts/plotSim.py"

rule plotPersist:
	input: "results/persistence/{params}_pimage_{i}.csv"
	output: "results/plots/{params}_pimage_{i}_dim_0.pdf","results/plots/{params}_pimage_{i}_dim_1.pdf","results/plots/{params}_pimage_{i}_dim_2.pdf",
	conda: "envs/Python.yaml"
	script: "scripts/plotPersistence.py"

